/*buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:0.8.0'
    }
}*/

plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'maven-publish'
    id "org.ajoberstar.grgit" version "1.7.2"
}

group 'me.geek.tom'

String baseVersion = '1.0'

String gitRev = grgit.head().abbreviatedId
version = baseVersion + "-git.${gitRev}"

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://libraries.minecraft.net" }
}

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

dependencies {
    api 'com.mojang:brigadier:1.0.17'
    api 'net.dv8tion:JDA:4.2.0_191'
    api 'com.beust:jcommander:1.71'

    implementation 'com.google.guava:guava:29.0-jre'

    api 'com.jagrosh:jda-utilities-menu:3.0.4'
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jar {
    manifest {
        attributes([
                "Specification-Title": "JDA-Simple-Bot",
                "Specification-Vendor": "Tom_The_Geek",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${project.version}",
                "Implementation-Vendor" :"Tom_The_Geek",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Main-Class": "me.geek.tom.jdabots.api.startup.BotLoader"
        ])
    }
}

build.dependsOn('sourceJar')

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'me.geek.tom'
            artifactId = "${project.name}"
            version = "${project.version}"

            from components.java
            artifact(shadowJar) {
                classifier "all"
            }
            artifact(sourceJar) {
                classifier 'sources'
            }
        }
    }

    repositories {
        maven {
            name = "TomTheGeek-Repo"
            url = "http://maven-upload.tomthegeek.ml/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}
