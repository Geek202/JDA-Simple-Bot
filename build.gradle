buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:0.8.0'
    }
}

plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'maven-publish'
}

import org.ajoberstar.grgit.Grgit

group 'me.geek.tom'

String baseVersion = '1.0'

String gitRev = Grgit.open(project.file('.')).head().abbreviatedId
version = baseVersion + "-git.${gitRev}"

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://libraries.minecraft.net" }
}

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

dependencies {
    api 'com.mojang:brigadier:1.0.17'
    api 'net.dv8tion:JDA:4.2.0_188'
    api 'com.beust:jcommander:1.71'

    implementation 'com.google.guava:guava:29.0-jre'
}

jar {
    manifest {
        attributes([
                "Specification-Title": "JDA-Simple-Bot",
                "Specification-Vendor": "Tom_The_Geek",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${project.version}",
                "Implementation-Vendor" :"Tom_The_Geek",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Main-Class": "me.geek.tom.jdabots.api.startup.BotLoader"
        ])
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'me.geek.tom'
            artifactId = "${project.name}"
            version = "${project.version}"

            from components.java
            artifact(shadowJar) {
                classifier "all"
            }
        }
    }

    repositories {
        maven {
            name = "TomTheGeek-Repo"
            url = "http://maven-upload.tomthegeek.ml/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}
